%macro CLEAN_WORK;
proc sql;
select memname into: work_tables SEPARATED BY ' '
from SASHELP.VTABLE
where libname='WORK'
and memname <> 'CISA_STUDYPOP';

%do t=1 %to %sysfunc(countw(&work_tables));
drop table %sysfunc(scan(&work_tables, &t));;
%end;
quit;
%mend;

DATA CISA;
	SET SH026250.CISA_ITV_CHRT (IN=A) /*  2,557,346 ACO ENROLLEES */
	    SH026250.CISA_CTR_CHRT (IN=B) /* 21,867,140 NO ACO */;

	IF A THEN ACO = 'YES'; IF B THEN ACO = 'NO';
	IF BBENE_SEX_IDENT_CD ^= '0' AND BBENE_RACE_CD ^= '0';
	FEMALE = (BBENE_SEX_IDENT_CD = '2');
	WHITE = (BBENE_RACE_CD = '1');
	BLACK = (BBENE_RACE_CD = '2');
	HISPN = (BBENE_RACE_CD = '5');
	ASIAN = (BBENE_RACE_CD = '4');
	OTHER = (BBENE_RACE_CD IN ('3','6'));
	IF 0 < INPUT(BSTATE_CODE, 2.0) < 54 OR BSTATE_CODE IN ('97','98','99'); 
	/* 50 STATES + D.C., PUERTO RICO, VIRGIN ISLAND, SAIPAN, GUAM, AMERICAN SAMOA */ 
	
	REGION01 = (BSTATE_CODE IN ('07','20','22','30','41','47'));
	REGION02 = (BSTATE_CODE IN ('31','33','40','48'));
	REGION03 = (BSTATE_CODE IN ('08','09','21','39','49','51'));
	REGION04 = (BSTATE_CODE IN ('01','10','11','18','25','34','42','44'));
	REGION05 = (BSTATE_CODE IN ('14','15','23','24','36','52'));
	REGION06 = (BSTATE_CODE IN ('04','19','32','37','45'));
	REGION07 = (BSTATE_CODE IN ('16','17','26','28'));
	REGION08 = (BSTATE_CODE IN ('06','27','35','43','46','53'));
	REGION09 = (BSTATE_CODE IN ('03','05','12','29','97','98','99'));
	REGION10 = (BSTATE_CODE IN ('02','13','38','50'));
	
	OASI = (BBENE_ENTLMT_RSN_CURR = '0');
	DIB = (BBENE_ENTLMT_RSN_CURR = '1');
	ESRD = (BBENE_ENTLMT_RSN_CURR = '2');
	BOTH = (BBENE_ENTLMT_RSN_CURR = '3');
	LABEL REGION01 = 'BOSTON'
	      REGION02 = 'NEW YORK'
		  REGION03 = 'PHILADELPHIA'
		  REGION04 = 'ATLANTA'
		  REGION05 = 'CHICAGO'
		  REGION06 = 'DALLAS'
		  REGION07 = 'KANSAS CITY'
		  REGION08 = 'DENVER'
		  REGION09 = 'SAN FRANCISCO'
		  REGION10 = 'SEATTLE'
		  OASI = 'OLD AGE AND SURVIVORS INSURANCE'
		  DIB = 'DISABILITY INSURANCE BENEFIT'
		  ESRD = 'END-STAGE RENAL DISEASE'
		  BOTH = 'BOTH DIB AND ESRD';
	BZCTA_BELOW_PL = BZCTA_BELOW_PL/100;
	BZCTA_HIGH_SCHOOL = BZCTA_HIGH_SCHOOL/100;
	BZCTA_COLLEGE = BZCTA_COLLEGE/100;
RUN; /* N = 24,424,486 */
PROC CONTENTS DATA=CISA OUT=VN NOPRINT; RUN;

PROC LOGISTIC DATA=CISA DESCENDING;
	MODEL ACO =  REGION01 REGION02 REGION03 REGION04 REGION05 REGION06 REGION07 REGION08 REGION09
				 BBENE_AGE_AT_END_REF_YR WHITE BLACK HISPN ASIAN FEMALE OASI DIB ESRD
	             BAMI_FLAG BALZH_DEMEN_FLAG BATRIAL_FIB_FLAG BCATARACT_FLAG BCHRONICKIDNEY_FLAG
				 BCOPD_FLAG BCHF_FLAG BDIABETES_FLAG BGLAUCOMA_FLAG BHIP_FRACTURE_FLAG BISCHEMICHEART_FLAG
				 BDEPRESSION_FLAG BOSTEOPOROSIS_FLAG BRA_OA_FLAG BSTROKE_TIA_FLAG BCANCER_BREAST_FLAG
				 BCANCER_COLORECTAL_FLAG BCANCER_PROSTATE_FLAG BCANCER_LUNG_FLAG BCANCER_ENDOMETRIAL_FLAG
				 BANEMIA_FLAG BASTHMA_FLAG BHYPERL_FLAG BHYPERP_FLAG BHYPERT_FLAG BHYPOTH_FLAG
                 BPHYS_EVENTS BPTB_DRUG_EVENTS BHOP_VISITS BSNF_COV_DAYS BHOS_COV_DAYS BHH_VISITS BASC_EVENTS 
				 BEM_EVENTS BANES_EVENTS BDIALYS_EVENTS BOPROC_EVENTS BIMG_EVENTS BTEST_EVENTS BDME_EVENTS 
				 BOTHC_EVENTS BREADMISSIONS BPTD_FILL_CNT BER_VISITS BINPAT_DAYS BMEDICARE_PMTS BBENEFICIARY_PMTS
				 /LINK=LOGIT RSQUARE;
	OUTPUT OUT=CISA_PS PRED= PS;
RUN;
DATA SH026250.CISA_STUDYPOP_PS; 
	SET CISA_PS;
	IF ACO = 'YES' THEN PS_WGT = 1/PS; 
	ELSE IF ACO = 'NO' THEN PS_WGT = 1/(1-PS);
RUN;
PROC CONTENTS DATA=CISA_PS OUT=VNAME NOPRINT; RUN;
PROC SORT DATA= VNAME (KEEP=NAME VARNUM); BY VARNUM; RUN;
DATA VNAME; 
	SET VNAME; 
	IF (VARNUM = 3) OR (8 < VARNUM < 56) OR (58 < VARNUM < 62) OR (62 < VARNUM < 83);
	FLAG = (VARNUM = 3 OR (34 < VARNUM < 56) OR (58 < VARNUM < 62));
RUN;

DATA _NULL_;
	SET VNAME;
	CALL SYMPUT(("COV"||COMPRESS(_N_)), NAME);
	CALL SYMPUT(('J'||COMPRESS(_N_)),FLAG);
	CALL SYMPUT('N',_N_);
RUN;
%MACRO CHAR(NAME);
%LOCAL I;
%LET I = 1;
%DO I = 1 %TO &N;
		
	PROC MEANS DATA=SH026250.CISA_STUDYPOP_PS; VAR &&COV&I; OUTPUT OUT=TAB0 MEAN=M; RUN;
	DATA TAB0; SET TAB0; OVERALL = ROUND(M,0.0001); RUN;
	/* NO PS WEIGHTING */
	ODS OUTPUT LSMEANS = TAB (KEEP = ACO LSMEAN PROBTDIFF RENAME = (PROBTDIFF = P_VALUE)); 
	PROC GLM DATA=SH026250.CISA_STUDYPOP_PS PLOTS=NONE; 
		CLASS ACO; 
		MODEL &&COV&I = ACO/SOLUTION; 
		LSMEANS ACO/PDIFF=CONTROL; 
	RUN;
	PROC TRANSPOSE DATA=TAB OUT=TAB1 (DROP=_NAME_ _LABEL_); VAR LSMEAN; ID ACO; RUN;
	DATA P1; SET TAB (WHERE=(ACO = 'NO') KEEP= ACO P_VALUE); RUN;

	/* PS WEIGHTING */
	ODS OUTPUT LSMEANS = TAB (KEEP = ACO LSMEAN PROBTDIFF RENAME = (PROBTDIFF = P_VALUE)); 
	PROC GLM DATA=SH026250.CISA_STUDYPOP_PS PLOTS=NONE; 
		CLASS ACO; 
		MODEL &&COV&I = ACO/SOLUTION; 
		LSMEANS ACO/PDIFF=CONTROL /*ADJUST=SIMULATE*/; 
		WEIGHT PS_WGT; 
	RUN;
	PROC TRANSPOSE DATA=TAB OUT=TAB2 (DROP=_NAME_ _LABEL_); VAR LSMEAN; ID ACO; RUN;
	DATA P2; SET TAB (WHERE=(ACO = 'NO') KEEP= ACO P_VALUE); RUN;	

	DATA TAB3; 
		LENGTH VARIABLE $20.;
		MERGE TAB0 (KEEP=OVERALL) TAB1(RENAME= (NO = NPS_NO YES = NPS_YES)) P1 (DROP=ACO RENAME=(P_VALUE=P_VALUE1))
		      TAB2 (RENAME= (NO = YPS_NO YES = YPS_YES)) P2 (DROP=ACO RENAME=(P_VALUE=P_VALUE2));
		VARIABLE = "&&COV&I"; 
	RUN;
	%IF &&J&I ^= 1 %THEN %DO;
	DATA TAB3;  SET TAB3;
	ARRAY V[*] OVERALL NPS_NO NPS_YES YPS_NO YPS_YES;
	DO K=1 TO 5;
	V[K] = 100*V[K];
	END;
	DROP K;
	RUN;
	%END;

	%IF &I=1 %THEN %DO;
		DATA SH026250.CISA_&NAME;
			LENGTH VARIABLE $25.;
			SET TAB3;
		RUN;
	%END;

	%IF &I^=1 %THEN %DO;
		DATA SH026250.CISA_&NAME;
			LENGTH VARIABLE $25.;
			SET SH026250.CISA_&NAME TAB3;
		RUN;
	%END;
%END;
%MEND;
%CHAR(TAB1);


/*** HEALTH OUTCOMES ***/
DATA CISA_POST; SET SH026250.CISA_ITV_OUT_MBSF SH026250.CISA_CTR_OUT_MBSF; RUN;
PROC SORT DATA=CISA_POST OUT=CISA_POST NODUPKEY; BY BENE_ID; RUN;
PROC SQL;
	CREATE TABLE CISA_STUDYPOP AS
	SELECT A.BENE_ID, ACO, REGION01, REGION02, REGION03, REGION04, REGION05, REGION06, 
		   REGION07, REGION08, REGION09, REGION10, WHITE, BLACK, HISPN, ASIAN, OTHER, FEMALE, 
		   OASI, DIB, ESRD, BOTH, B.ABENE_AGE_AT_END_REF_YR, AAMI_FLAG, AALZH_DEMEN_FLAG, 
		   AATRIAL_FIB_FLAG, ACATARACT_FLAG, ACHRONICKIDNEY_FLAG, ACOPD_FLAG, ACHF_FLAG, 
		   ADIABETES_FLAG, AGLAUCOMA_FLAG, AHIP_FRACTURE_FLAG, AISCHEMICHEART_FLAG, 
		   ADEPRESSION_FLAG, AOSTEOPOROSIS_FLAG, ARA_OA_FLAG, ASTROKE_TIA_FLAG, 
		   ACANCER_BREAST_FLAG, ACANCER_COLORECTAL_FLAG, ACANCER_PROSTATE_FLAG,
		   ACANCER_LUNG_FLAG, ACANCER_ENDOMETRIAL_FLAG, AANEMIA_FLAG, AASTHMA_FLAG, 
		   AHYPERL_FLAG, AHYPERP_FLAG, AHYPERT_FLAG, AHYPOTH_FLAG, A.PS_WGT, BZCTA_BELOW_PL,
     	   BZCTA_HIGH_SCHOOL, BZCTA_COLLEGE
	FROM SH026250.CISA_STUDYPOP_PS AS A, CISA_POST AS B
	WHERE A.BENE_ID = B.BENE_ID
	ORDER BY A.BENE_ID;
QUIT;
%MACRO TAB2(IN1,IN2,OUT);
DATA CISA_OUT; SET SH026250.CISA_ITV_&IN1._&IN2 SH026250.CISA_CTR_&IN1._&IN2; RUN;
PROC SORT DATA=CISA_OUT OUT=CISA_OUT NODUPKEY; BY BENE_ID; RUN;
PROC CONTENTS DATA=CISA_OUT OUT=VNAMES (KEEP= NAME VARNUM) ; RUN;
PROC SORT DATA=VNAMES; BY VARNUM; RUN;
	%IF &IN2 = MBSF %THEN %DO;
		DATA VNAMES_&OUT; SET VNAMES;
			IF VARNUM IN (2,91) OR 33 < VARNUM < 90 ; 
		RUN;
	%END;
	%ELSE %IF &IN2 ^= MBSF %THEN %DO;
		DATA VNAMES_&OUT; SET VNAMES;
			IF VARNUM > 1; 
		RUN;
	%END;

PROC SQL;
	CREATE TABLE CISA_OUT_STUDYPOP AS
	SELECT *
	FROM CISA_STUDYPOP AS A, CISA_OUT AS B
	WHERE A.BENE_ID = B.BENE_ID
	ORDER BY A.BENE_ID;
QUIT;
DATA _NULL_;
	SET VNAMES_&OUT;
	CALL SYMPUT(("VAR"||COMPRESS(_N_)), NAME);
	CALL SYMPUT('N',_N_);
RUN;

%LOCAL I;
%LET I = 1;
%DO I = 1 %TO &N;

	ODS OUTPUT LSMEANS = TAB (KEEP = ACO LSMEAN PROBTDIFF RENAME = (PROBTDIFF = P_VALUE)); 
	PROC GLM DATA=CISA_OUT_STUDYPOP PLOTS=NONE; 
		CLASS ACO; 
		MODEL &&VAR&I = ACO REGION01 REGION02 REGION03 REGION04 REGION05 REGION06 REGION07 REGION08 REGION09
				 ABENE_AGE_AT_END_REF_YR WHITE BLACK HISPN ASIAN FEMALE OASI DIB ESRD
	             AAMI_FLAG AALZH_DEMEN_FLAG AATRIAL_FIB_FLAG ACATARACT_FLAG ACHRONICKIDNEY_FLAG
				 ACOPD_FLAG ACHF_FLAG ADIABETES_FLAG AGLAUCOMA_FLAG AHIP_FRACTURE_FLAG AISCHEMICHEART_FLAG
				 ADEPRESSION_FLAG AOSTEOPOROSIS_FLAG ARA_OA_FLAG ASTROKE_TIA_FLAG ACANCER_BREAST_FLAG
				 ACANCER_COLORECTAL_FLAG ACANCER_PROSTATE_FLAG ACANCER_LUNG_FLAG ACANCER_ENDOMETRIAL_FLAG
				 AANEMIA_FLAG AASTHMA_FLAG AHYPERL_FLAG AHYPERP_FLAG AHYPERT_FLAG AHYPOTH_FLAG
 				 BZCTA_BELOW_PL BZCTA_COLLEGE/SOLUTION; 
		LSMEANS ACO/DIFF=CONTROL; 
		WEIGHT PS_WGT; 
	RUN;
	PROC TRANSPOSE DATA=TAB OUT=TAB1 (DROP=_NAME_ _LABEL_); VAR LSMEAN; ID ACO; RUN;
	DATA P; SET TAB (WHERE=(ACO = 'NO') KEEP= ACO P_VALUE); RUN;

	PROC SQL;
		CREATE TABLE TAB2 AS
		SELECT "&&VAR&I" AS VARIABLE FORMAT=$20., A.NO, A.YES, A.NO - A.YES AS DIFF, B.P_VALUE
		FROM TAB1 AS A, P AS B;
	QUIT;
	
	%IF &I=1 %THEN %DO;
		DATA SH026250.CISA_TAB2_&OUT;
			SET TAB2;
		RUN;
	%END;

	%IF &I^=1 %THEN %DO;
		DATA SH026250.CISA_TAB2_&OUT;
			SET SH026250.CISA_TAB2_&OUT TAB2;
		RUN;
	%END;
%END;
%MEND; 

/* DIFFERENCE IN DIFFERENCE */
%TAB2(D,MEDP,D_MEDP);%CLEAN_WORK;
%TAB2(D,INP ,D_INP);%CLEAN_WORK;
%TAB2(D,SNF ,D_SNF);%CLEAN_WORK;
%TAB2(D,CLMS,D_CLMS);%CLEAN_WORK;
%TAB2(D,MBSF,D_MBSF);%CLEAN_WORK;
%TAB2(D,DRUG,D_DRUG);%CLEAN_WORK;
